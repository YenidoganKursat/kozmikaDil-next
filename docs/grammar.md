# Phase 2 Syntax (Canonical)

The language uses Python-like indentation for blocks and Python-flavored statement syntax.

## 1) Statements

```ebnf
program        -> statement*
statement      -> class_def | function_def | async_function_def | with_task_group_stmt
               | if_stmt | while_stmt | for_stmt | async_for_stmt
               | return_stmt | assignment | expression_stmt
class_def      -> "class" IDENTIFIER [ "(" ( "open" | "slots" ) ")" ] ":" NEWLINE INDENT block
function_def   -> "def" IDENTIFIER "(" [param_list] ")" ":" NEWLINE INDENT block
async_function_def -> ("async def" | "async fn") IDENTIFIER "(" [param_list] ")" ":" NEWLINE INDENT block
with_task_group_stmt -> "with task_group" ["(" expression ")"] "as" IDENTIFIER ":" NEWLINE INDENT block
param_list     -> IDENTIFIER ("," IDENTIFIER)*
return_stmt    -> "return" [expression]
if_stmt        -> "if" expression ":" NEWLINE INDENT block {"elif" expression ":" NEWLINE INDENT block} ["else" ":" NEWLINE INDENT block]
while_stmt     -> "while" expression ":" NEWLINE INDENT block
for_stmt       -> "for" IDENTIFIER "in" expression ":" NEWLINE INDENT block
async_for_stmt -> "async for" IDENTIFIER "in" expression ":" NEWLINE INDENT block
assignment     -> IDENTIFIER "=" expression
expression_stmt -> expression
block          -> (statement NEWLINE)*
```

## 2) Expressions

```ebnf
expression     -> or_expr
or_expr        -> and_expr ("or" and_expr)*
and_expr       -> equality_expr ("and" equality_expr)*
equality_expr  -> comparison_expr (("==" | "!=") comparison_expr)*
comparison_expr-> term (("<" | "<=" | ">" | ">=") term)*
term           -> factor (("+" | "-") factor)*
factor         -> power (("*" | "/" | "%") power)*
power          -> unary ("^" power)?
unary          -> ("-" | "not" | "await") unary | numeric_ctor_prefix | call_or_atom
numeric_ctor_prefix -> NUMERIC_CTOR unary
call_or_atom   -> atom (call_or_index)*
call_or_index  -> "(" [arg_list] ")" | "[" expression "]" | "." IDENTIFIER
arg_list       -> expression ("," expression)*
atom           -> IDENTIFIER | NUMBER | STRING | TRUE | FALSE | list_literal | "(" expression ")"
list_literal   -> "[" list_items "]"
list_items     -> [ expression_list | matrix_rows ]
expression_list-> expression ("," expression)*
matrix_rows    -> list_expr (";" list_expr)*
```

### Matrix syntax policy

- Parser accepts both styles:
  - Python-style: `[[1,2],[3,4]]`
  - Semicolon-style row separator (legacy): `[[1,2];[3,4]]`
- Both are normalized into the same AST form (`ListExpr` where each row is itself a `ListExpr`), so parser ambiguity is gone and downstream tools can rely on one canonical representation.

### Lexing / tokens

- `IDENTIFIER`: `[A-Za-z_][A-Za-z0-9_]*`
- `NUMBER`: integer and float literals
- `STRING`: single or double quoted literal with backslash escapes
- `NUMERIC_CTOR`: `i8|i16|i32|i64|i128|i256|i512|f8|f16|bf16|f32|f64|f128|f256|f512|string`
- `TRUE` / `FALSE`
- unary keywords: `not`, `await`
- comments: `# ...` till end of line

### Numeric constructor sugar

- Both forms are valid and canonicalize to the same AST call form:
  - `x = f512(1.25)`
  - `x = f512 1.25`

### String/Unicode runtime builtins

- `len(s)` for string values returns Unicode codepoint count.
- `utf8_len(s)` returns UTF-8 byte length.
- `utf16_len(s)` returns UTF-16 code-unit length.

### Concurrency Runtime Builtins (Phase 9)

- Task primitives:
  - `spawn(fn, ...)`
  - `join(task [, timeout_ms])`
  - `deadline(timeout_ms)` (timeout/deadline alias helper)
  - `cancel(task)`
  - `task_group([timeout_ms])`
- Parallel primitives:
  - `parallel_for(start, stop, fn [, extra...])`
  - `par_map(list, fn)`
  - `par_reduce(list, init, fn)`
- Event-driven primitives:
  - `channel([capacity])`, `send(ch, v)`, `recv(ch [, timeout_ms])`, `close(ch)`
  - `stream(ch)`, `anext(ch_or_stream [, timeout_ms])`
  - channel methods: `.send()`, `.recv()`, `.anext()`, `.has_next()`, `.close()`, `.stats()`

### Phase 7 method-chain note

Phase 7 ile birlikte method-chain pipeline bi√ßimi semantik olarak aktiftir:

- `x.map_add(1).filter_gt(2).reduce_sum()`
- `x.map_add(1).scan_sum()`
- `m.map_mul(2).reduce_sum()`
